buildscript {
	ext {
		springBootVersion = '2.0.2.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE"
		classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
	}
}

allprojects {
	apply plugin: 'com.github.kt3k.coveralls'
	apply plugin: 'jacoco'

	repositories {
		mavenCentral()
		jcenter()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'maven'
	apply plugin: 'eclipse'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.github.jojoldu'
	version = '0.3.1'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	artifacts {
		archives sourcesJar
		archives javadocJar
	}

	repositories {
		mavenCentral()
		jcenter()
		maven { url 'https://repo.spring.io/libs-milestone' }
	}

	dependencies {
		testCompile('org.springframework.boot:spring-boot-starter-test')
	}

	dependencyManagement {
		imports {
			mavenBom 'org.springframework.cloud:spring-cloud-aws:2.0.0.RC1'
		}
	}

	jacocoTestReport {
		reports {
			html.enabled = true // human readable
			xml.enabled = true // required by coveralls
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.5.1'
}

task jacocoRootReport(type: JacocoReport) {
	description = 'Generates an aggregate report from all subprojects'
	dependsOn = subprojects.test
	sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
	classDirectories =  files(subprojects.sourceSets.main.output)
	executionData = files(subprojects.jacocoTestReport.executionData)
	reports {
		html.enabled = true // human readable
		xml.enabled = true // required by coveralls
	}
}

coveralls {
	sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
	jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}
